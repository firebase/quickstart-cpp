// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  repositories {
    mavenLocal()
    maven { url 'https://maven.google.com'  }
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:2.1.2'
    classpath 'com.google.gms:google-services:3.0.0'
  }
}

allprojects {
  repositories {
    mavenLocal()
    maven { url 'https://maven.google.com'  }
    jcenter()
  }
}

apply plugin: 'com.android.application'

// Pre-experimental Gradle plug-in NDK boilerplate below.
// Right now the Firebase plug-in does not work with the experimental
// Gradle plug-in so we're using ndk-build for the moment.
project.ext {
  // Configure the Firebase C++ SDK location.
  firebase_cpp_sdk_dir = System.getProperty('firebase_cpp_sdk.dir')
  if (firebase_cpp_sdk_dir == null || firebase_cpp_sdk_dir.isEmpty()) {
    firebase_cpp_sdk_dir = System.getenv('FIREBASE_CPP_SDK_DIR')
    if (firebase_cpp_sdk_dir == null || firebase_cpp_sdk_dir.isEmpty()) {
      if ((new File('firebase_cpp_sdk')).exists()) {
        firebase_cpp_sdk_dir = 'firebase_cpp_sdk'
      } else {
        throw new StopActionException(
          'firebase_cpp_sdk.dir property or the FIREBASE_CPP_SDK_DIR ' +
          'environment variable must be set to reference the Firebase C++ ' +
          'SDK install directory.  This is used to configure static library ' +
          'and C/C++ include paths for the SDK.')
      }
    }
  }
  if (!(new File(firebase_cpp_sdk_dir)).exists()) {
    throw new StopActionException(
      sprintf('Firebase C++ SDK directory %s does not exist',
              firebase_cpp_sdk_dir))
  }
  // Check the NDK location using the same configuration options as the
  // experimental Gradle plug-in.
  ndk_dir = project.android.ndkDirectory
  if (ndk_dir == null || !ndk_dir.exists()) {
    ndk_dir = System.getenv('ANDROID_NDK_HOME')
    if (ndk_dir == null || ndk_dir.isEmpty()) {
      throw new StopActionException(
        'Android NDK directory should be specified using the ndk.dir ' +
        'property or ANDROID_NDK_HOME environment variable.')
    }
  }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    sourceSets {
      main {
        jniLibs.srcDirs = ['libs']
        manifest.srcFile 'AndroidManifest.xml'
        java.srcDirs = ['src/android/java']
        res.srcDirs = ['res']
      }
    }

    defaultConfig {
        applicationId 'com.google.firebase.cpp.database.testapp'
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
      release {
            minifyEnabled true
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile file(project.ext.firebase_cpp_sdk_dir + "/libs/android/app.pro")
            proguardFile file(project.ext.firebase_cpp_sdk_dir + "/libs/android/auth.pro")
            proguardFile file(project.ext.firebase_cpp_sdk_dir + "/libs/android/database.pro")
            proguardFile file('proguard.pro')
      }
    }
}

dependencies {
    compile 'com.google.firebase:firebase-auth:11.2.0'
    compile 'com.google.firebase:firebase-database:11.2.0'
}

apply plugin: 'com.google.gms.google-services'

task ndkBuildCompile(type:Exec) {
  description 'Use ndk-build to compile the C++ application.'
  commandLine("${project.ext.ndk_dir}${File.separator}ndk-build",
              "FIREBASE_CPP_SDK_DIR=${project.ext.firebase_cpp_sdk_dir}",
              sprintf("APP_PLATFORM=android-%d",
                      android.defaultConfig.minSdkVersion.mApiLevel))
}

task ndkBuildClean(type:Exec) {
  description 'Use ndk-build to clean the C++ application.'
  commandLine("${project.ext.ndk_dir}${File.separator}ndk-build",
              "FIREBASE_CPP_SDK_DIR=${project.ext.firebase_cpp_sdk_dir}",
              "clean")
}

// Once the Android Gradle plug-in has generated tasks, add dependencies for
// the ndk-build targets.
project.afterEvaluate {
  preBuild.dependsOn(ndkBuildCompile)
  clean.dependsOn(ndkBuildClean)
}
